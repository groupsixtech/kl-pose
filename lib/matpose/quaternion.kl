PROGRAM quaternion
%NOBUSYLAMP
%COMMENT = ''
%UNINITVARS
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR

%include errors.klt
%include quaternion.klt

--matrix type
%include define_type.m
 -- 4 x 4
%include matarr.klt
t_arr2d_ref(t_rows,t_columns,type_name,inner_type)

%from errors.klh %import CHK_STAT, karelError 
%from strings.klh %import i_to_s, r_to_s
%from kunit.klh %import kunit_eq_r
%include quaternion.klh

--matrix class
%ifdef DEBUG_BUILD
 -- use for testing
%class cmat('2dArray.klc','2dArray.klh','matarr_test.klt')
%else
 -- use for production
%class cmat('2dArray.klc','2dArray.klh','matarr.klt')
%endif


--quaternions
ROUTINE quaternion__set
  VAR
    out : T_QUAT
  BEGIN
    out.w = w ; out.x = x ; out.y = y ; out.z = z;
    RETURN(out)
  END quaternion__set

ROUTINE quaternion__conj
  VAR
    out : T_QUAT
  BEGIN
    out.w = q.w ; out.x = -q.x ; out.y = -q.y ; out.z = -q.z;
    RETURN(out)
  END quaternion__conj

ROUTINE quaternion__normalize
  VAR
    out_quat : T_QUAT
    norm :REAL
  BEGIN
    norm = SQRT(q.w*q.w + q.x*q.x + q.y*q.y + q.z*q.z)

    out_quat.w = q.w/norm
    out_quat.x = q.x/norm
    out_quat.y = q.y/norm
    out_quat.z = q.z/norm

    RETURN(out_quat)
  END quaternion__normalize

ROUTINE quaternion__mult
  VAR
    outq : T_QUAT
  BEGIN
    outq.w = q1.w*q2.w - q1.x*q2.x - q1.y*q2.y - q1.z*q2.z
    outq.x = q1.w*q2.x + q1.x*q2.w + q1.y*q2.z - q1.z*q2.y
    outq.y = q1.w*q2.y - q1.x*q2.z + q1.y*q2.w + q1.z*q2.x
    outq.z = q1.w*q2.z + q1.x*q2.y - q1.y*q2.x + q1.z*q2.w

    RETURN(outq)
    
  END quaternion__mult


--quaternions conversion

ROUTINE max_val_(v1, v2 : inner_type) : inner_type
  BEGIN
    IF v1 > v2 THEN
      RETURN(v1)
    ENDIF

    RETURN(v2)
  END max_val_ 

ROUTINE quaternion__mat_to_quat
  VAR
    sign2, sign3, sign4 : INTEGER
    q : T_QUAT
  BEGIN
    sign2 = 1; sign3 = 1; sign4 = 1;

    IF (mat[3,2]-mat[2,3] < 0) THEN sign2 = -1; ENDIF
    IF (mat[1,3]-mat[3,1] < 0) THEN sign3 = -1; ENDIF
    IF (mat[2,1]-mat[1,2] < 0) THEN sign4 = -1; ENDIF

    q.w = SQRT(max_val_(mat[1,1]+mat[2,2]+mat[3,3]+1, 0))/2
    q.x = sign2*SQRT(max_val_(mat[1,1]-mat[2,2]-mat[3,3]+1, 0))/2
    q.y = sign3*SQRT(max_val_(-1*mat[1,1]+mat[2,2]-mat[3,3]+1, 0))/2
    q.z = sign4*SQRT(max_val_(-1*mat[1,1]-mat[2,2]+mat[3,3]+1, 0))/2

    RETURN(q)
  END quaternion__mat_to_quat

ROUTINE quaternion__quat_to_mat
  VAR
    qnorm : T_QUAT
  BEGIN
    qnorm = quaternion__normalize(q)

    out_mat[1,1] = 1-2*q.y*q.y - 2*q.z*q.z ; out_mat[1,2] = 2*q.x*q.y - 2*q.z*q.w ; out_mat[1,3] = 2*q.x*q.z + 2*q.y*q.w ; out_mat[1,4] = 0
    out_mat[2,1] = 2*q.x*q.y + 2*q.z*q.w ; out_mat[2,2] = 1-2*q.x*q.x - 2*q.z*q.z ; out_mat[2,3] = 2*q.y*q.z - 2*q.x*q.w ; out_mat[2,4] = 0
    out_mat[3,1] = 2*q.x*q.z - 2*q.y*q.w ; out_mat[3,2] = 2*q.y*q.z + 2*q.x*q.w ; out_mat[3,3] = 1-2*q.x*q.x - 2*q.y*q.y ; out_mat[3,4] = 0
    out_mat[4,1] = 0 ; out_mat[4,2] = 0 ; out_mat[4,3] = 0 ; out_mat[4,4] = 1

  END quaternion__quat_to_mat

ROUTINE quaternion__pose_to_quat
  VAR
    c : VECTOR
    s : VECTOR
    outq : T_QUAT
  BEGIN
    c.x = COS(p.w*0.5)
    s.x = SIN(p.w*0.5)
    c.y = COS(p.p*0.5)
    s.y = SIN(p.p*0.5)
    c.z = COS(p.r*0.5)
    s.z = SIN(p.r*0.5)

    outq.w = c.x*c.y*c.z + s.x*s.y*s.z
    outq.x = s.x*c.y*c.z - c.x*s.y*s.z
    outq.y = c.x*s.y*c.z + s.x*c.y*s.z
    outq.z = c.x*c.y*s.z - s.x*s.y*c.z

    RETURN(outq)

  END quaternion__pose_to_quat

ROUTINE quaternion__quat_to_pose
  VAR
    out : XYZWPR
    x, y : REAL
  BEGIN
    out.x = 0 ; out.y = 0 ; out.z = 0

    x = 1-2*(q.x*q.x + q.y*q.y) ; y = 2*(q.w*q.x + q.y*q.z)
    IF (x = 0) AND (y = 0) THEN out.w = 0 ;
    ELSE out.w = ATAN2(x, y) ; ENDIF

    x = SQRT(1-2*(q.w*q.y - q.x*q.z)) ; y = SQRT(1+2*(q.w*q.y - q.x*q.z))
    IF (x = 0) AND (y = 0) THEN out.p = 0 ;
    ELSE out.p = -90 + 2*ATAN2(x, y) ; ENDIF

    x = 1-2*(q.y*q.y+q.z*q.z) ; y = 2*(q.w*q.z + q.y*q.x)
    IF (x = 0) AND (y = 0) THEN out.r = 0 ;
    ELSE out.r = ATAN2(x, y) ; ENDIF

    RETURN(out)
  END quaternion__quat_to_pose

-- testing

ROUTINE quaternion__test
  VAR
    b : BOOLEAN
    idx : INTEGER
    actr, expr : REAL
    ent, status : INTEGER
    error_msg : STRING[127]
  BEGIN
    b = FALSE
    idx = 0

    b = kunit_eq_r(expected.w, actual.w)
    IF NOT b THEN idx = 1; actr = actual.w; expr = expected.w; ENDIF
    b = kunit_eq_r(expected.x, actual.x)
    IF NOT b THEN idx = 2; actr = actual.x; expr = expected.x; ENDIF
    b = kunit_eq_r(expected.y, actual.y)
    IF NOT b THEN idx = 3; actr = actual.y; expr = expected.y; ENDIF
    b = kunit_eq_r(expected.z, actual.z)
    IF NOT b THEN idx = 4; actr = actual.z; expr = expected.z; ENDIF

    IF idx > 0 THEN
      error_msg = 'quaternion index ' + i_to_s(idx) + ' is not equal.'
      error_msg = error_msg + ' got ' + r_to_s(actr) + ' but expected ' + r_to_s(expr)
      SET_VAR(ent, 'kunit', 'error_msg', error_msg, status)
      CHK_STAT(status)
      RETURN(FALSE)
    ENDIF

    RETURN(b)
  END quaternion__test


BEGIN
END quaternion

